apply plugin: 'com.android.library'
apply plugin: 'kotlin-platform-android'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        android(MavenPublication) {
            artifact("$buildDir/outputs/aar/android-release.aar")
            artifact sourcesJar
            artifactId 'kissme-android'

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

android {
    compileSdkVersion androidCompileSdkVersion
    defaultConfig {
        minSdkVersion androidMinSdkVersion
    }
    testOptions.unitTests.includeAndroidResources = true

    sourceSets.all {
        it.java.srcDir "src/$it.name/kotlin"
    }

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    expectedBy project(':common')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // compile type binaryprefs dependency is needed to specify that 
    // it should be included in the .pom file of the kissme-android artifact
    // TODO: find a better way to declare it
    compile "com.github.yandextaxitech:binaryprefs:1.0.1"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "io.mockk:mockk:1.8.13.kotlin13"
    testImplementation "androidx.test:core:1.1.0"
    testImplementation "androidx.test.ext:junit:1.1.0"
    testImplementation "org.robolectric:robolectric:4.0"
}
